apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

group 'com.assertthat'
archivesBaseName = "selenium-shutterbug"
version '0.9.1'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    testCompile 'junit:junit:4.12'
    compile group: 'org.seleniumhq.selenium', name: 'selenium-java', version: '3.+'
    compile group: 'commons-io', name: 'commons-io', version: '2.6'
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: "$System.env.OSSRH_JIRA_USERNAME", password: "$System.env.OSSRH_JIRA_PASSWORD")
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: "$System.env.OSSRH_JIRA_USERNAME", password: "$System.env.OSSRH_JIRA_PASSWORD")
            }
            pom.project {
                name 'Selenium Shutterbug'
                packaging 'jar'

                description 'Utility library to create customized screenshots using Selenium'
                url 'https://github.com/glib-briia/selenium-shutterbug'

                scm {
                    connection 'scm:git:https://github.com/assertthat/selenium-shutterbug.git'
                    developerConnection 'scm:git:https://github.com/assertthat/selenium-shutterbug.git'
                    url 'https://github.com/assertthat/selenium-shutterbug/'
                }

                licenses {
                    license {
                        name 'The MIT License (MIT)'
                        url 'https://github.com/assertthat/selenium-shutterbug/blob/master/LICENSE'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'glib-briia'
                        name 'Glib Briia'
                        email 'glib.briia@assertthat.com'
                    }
                }
            }
        }
    }
}

test {
    testLogging {
        events "passed", "skipped", "failed"
        exceptionFormat = 'full'
    }
}

signing {
    required { gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}
